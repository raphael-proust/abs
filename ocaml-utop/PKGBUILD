# Maintainer: Taylor Venable <taylor@metasyntax.net>

srcname='utop'
pkgname="ocaml-${srcname}"
pkgver=1.1
pkgrel=1
pkgdesc='A toplevel for OCaml that supports completion, colors, and parenthesis matching'
arch=('i686' 'x86_64')
url='http://forge.ocamlcore.org/projects/utop/'
license=('BSD')
depends=('ocaml' 'ocaml-lwt' 'ocaml-lambda-term')
makedepends=('ocaml-findlib')
source=('https://forge.ocamlcore.org/frs/download.php/674/utop-1.1.tar.gz')
options=('!strip')
md5sums=('dc60c8449cb05eb1f85dd99222034979')

build() {
    cd "$srcdir/${srcname}-${pkgver}"
    ./configure --prefix /usr --destdir "$pkgdir"

    env DESTDIR="$pkgdir" \
        OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
        make
}


package() {
    mkdir -p "$pkgdir/$(ocamlfind printconf destdir)"
    mkdir -p "$pkgdir/$(ocamlfind printconf destdir)/stublibs"
    cd "$srcdir/${srcname}-${pkgver}"
    env DESTDIR="$pkgdir" \
        OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
        make install
    install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
